REM BLLRDS.BAS
REM Billiards

USEGRAPHIC 5
USECLASS CRDINI

USEVAR LCDTURN
CRDINI::INIT()
IF CRDINI::ISSET("HORIZONTAL") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("VERTICAL") THEN
  LCDTURN=270
ELSEIF CRDINI::ISSET("LCD0TURN") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("LCD90TURN") THEN
  LCDTURN=90
ELSEIF CRDINI::ISSET("LCD180TURN") THEN
  LCDTURN=180
ELSEIF CRDINI::ISSET("LCD270TURN") THEN
  LCDTURN=270
ELSE
  LCDTURN=0
ENDIF

USEVAR X1,X2,Y1,Y2,XMIN,XMAX,YMIN,YMAX
XMIN=0X17A:XMAX=0XECB:YMIN=0X11A:YMAX=0XE81

USEVAR INITF,BGN,BLC,STC,BKC,EGC,PEN,WMAX,HMAX,BMPW,BMPH,EDG,BLR

USEVAR DX,DY,STP,XO,YO,LXD,LYD,ZD,LPX,LPY,LPXO,LPYO,DIX,DIY,CNT

CLS
SPI 2500
INITF$="TOUCHPAD.INI"

IF FOPEN(INITF$,"R") THEN
  XMIN=VAL(FINPUT$())
  XMAX=VAL(FINPUT$())
  YMIN=VAL(FINPUT$())
  YMAX=VAL(FINPUT$())
  FCLOSE
ELSE
  GOSUB MINMAX
ENDIF

BGN=0
PEN=1

GPALETTE 16,$29,$91,$46
GPALETTE 17,$41,$31,$0F

STC=0
BLC=7
BKC=16
EGC=17

BLR=5
EDG=10

WMAX=SYSTEM(22)
HMAX=SYSTEM(23)
BMPW=300:BMPH=240
DIX=1:DIY=1

XO=WMAX/2-1: YO=HMAX/2-1
LPX=XO: LPY=YO
X=0: Y=0

BOXFILL 0,0,WMAX-1,HMAX-1,EGC
BOXFILL EDG,EDG,WMAX-EDG-1,HMAX-EDG-1,BKC
CIRCLEFILL EDG,EDG,EDG,0
CIRCLEFILL WMAX/2-1,EDG,EDG,0
CIRCLEFILL WMAX-EDG-1,EDG,EDG,0
CIRCLEFILL EDG,HMAX-EDG-1,EDG,0
CIRCLEFILL WMAX/2-1,HMAX-EDG-1,EDG,0
CIRCLEFILL WMAX-EDG-1,HMAX-EDG-1,EDG,0

CIRCLEFILL LPX,LPY,BLR,BLC

DO
  IF IN(0) THEN
    IF BGN=0 AND X!=0 AND Y!=0 THEN 
rem      PRINT "END"
      SOUND SND0
      LINE LPX,LPY,X,Y,BKC
      LXD#=FLOAT#(X-LPX)
      LYD#=FLOAT#(Y-LPY)
      I=1
      ZD#=SQRT#(LXD#*LXD#+LYD#*LYD#)
      CNT=INT(ZD#)*2
      DO WHILE CNT > 0
        LPX=XO-1*INT(LXD#*FLOAT#(I)/ZD#)*DIX
        LPY=YO-1*INT(LYD#*FLOAT#(I)/ZD#)*DIY
        IF (LPX < EDG+BLR) OR (LPX >= WMAX-(EDG+BLR)) OR (LPY < EDG+BLR) OR (LPY >= HMAX-(EDG+BLR)) THEN
          SOUND SND1
          IF LPX < EDG+BLR THEN
            IF (LPY < EDG+EDG) OR (LPY >= HMAX-(EDG+EDG)) THEN
              CNT=0
              CIRCLEFILL LPXO,LPYO,BLR,BKC
              SOUND SND2
              CIRCLEFILL LPX,LPY,BLR,BLC+8
              GOSUB SLEEP,600000
              CIRCLEFILL LPX,LPX,BLR,BKC
              BREAK
            ENDIF
            DIX=-DIX
            LPX=EDG+BLR
            XO=EDG+BLR
            YO=LPY
          ENDIF
          IF LPX >= WMAX-(EDG+BLR) THEN
            IF (LPY < EDG+EDG) OR (LPY >= HMAX-(EDG+EDG)) THEN
              CNT=0
              CIRCLEFILL LPXO,LPYO,BLR,BKC
              SOUND SND2
              CIRCLEFILL LPX,LPY,BLR,BLC+8
              GOSUB SLEEP,600000
              CIRCLEFILL LPX,LPX,BLR,BKC
              BREAK
            ENDIF
            DIX=-DIX
            LPX=WMAX-(EDG+BLR)-1
            XO=WMAX-(EDG+BLR)-1
            YO=LPY
          ENDIF
          IF LPY < EDG+BLR THEN
            IF (LPX < EDG+EDG) OR (LPX >= WMAX-(EDG+EDG)) OR (LPX >= WMAX/2-EDG) AND (LPX < WMAX/2+EDG)THEN
              CNT=0
              CIRCLEFILL LPXO,LPYO,BLR,BKC
              SOUND SND2
              CIRCLEFILL LPX,LPY,BLR,BLC+8
              GOSUB SLEEP,600000
              CIRCLEFILL LPX,LPX,BLR,BKC
              BREAK
            ENDIF
            DIY=-DIY
            LPY=EDG+BLR
            XO=LPX
            YO=EDG+BLR
          ENDIF
          IF LPY >= HMAX-(EDG+BLR) THEN
            IF (LPX < EDG+EDG) OR (LPX >= WMAX-(EDG+EDG)) OR (LPX >= WMAX/2-EDG) AND (LPX < WMAX/2+EDG)THEN
              CNT=0
              CIRCLEFILL LPXO,LPYO,BLR,BKC
              SOUND SND2
              CIRCLEFILL LPX,LPY,BLR,BLC+8
              GOSUB SLEEP,600000
              CIRCLEFILL LPX,LPX,BLR,BKC
              BREAK
            ENDIF
            DIY=-DIY
            LPY=HMAX-(EDG+BLR)-1
            XO=LPX
            YO=HMAX-(EDG+BLR)-1
          ENDIF
          I=1
        ELSE
          CIRCLEFILL LPXO,LPYO,BLR,BKC
          CIRCLEFILL LPX,LPY,BLR,BLC
          GOSUB SLEEP, INT(10000*(FLOAT#(WMAX)-ZD#*2)/FLOAT#(WMAX))*INT(ZD#)/CNT
          LPXO=LPX: LPYO=LPY
          I=I+1
        ENDIF
        CNT=CNT-1
      LOOP
        CIRCLEFILL EDG,EDG,EDG,0
        CIRCLEFILL WMAX/2-1,EDG,EDG,0
        CIRCLEFILL WMAX-EDG-1,EDG,EDG,0
        CIRCLEFILL EDG,HMAX-EDG-1,EDG,0
        CIRCLEFILL WMAX/2-1,HMAX-EDG-1,EDG,0
        CIRCLEFILL WMAX-EDG-1,HMAX-EDG-1,EDG,0
      XO=LPX:YO=LPY
      DIX=1:DIY=1
    ENDIF
    BGN=1
    CONTINUE
  ENDIF
  X1=GOSUB(TSCREAD,0X94)
  Y1=GOSUB(TSCREAD,0XD4)
  X2=GOSUB(TSCREAD,0X94)
  Y2=GOSUB(TSCREAD,0XD4)
  IF IN(0) THEN
     CONTINUE
  ENDIF
  IF ABS(X1-X2)>2 THEN CONTINUE
  IF ABS(Y1-Y2)>2 THEN CONTINUE
  
  LINE LPX,LPY,X,Y,BKC
  
  X=(X1+X2)>>1
  Y=(Y1+Y2)>>1
  X=WMAX*(X-XMIN)/(XMAX-XMIN)-1
  Y=HMAX*(Y-YMIN)/(YMAX-YMIN)-1

  IF 0=LCDTURN THEN
    REM
  ELSEIF 90=LCDTURN THEN
    REM
  ELSEIF 180=LCDTURN THEN
    IF BGN=1 THEN
      POINT X,Y
      BGN=0
rem      PRINT "START"
    ENDIF
    LINE LPX,LPY,X,Y,STC
    CIRCLEFILL LPX,LPY,BLR,BLC
  ELSE
    REM
  ENDIF
LOOP
END

LABEL SLEEP
  USETIMER ARGS(1)
  WHILE TIMER()=0
    IDLE
  WEND
RETURN

LABEL SND0
DATA $200CB,1

LABEL SND1
DATA $10320,$22580,1

LABEL SND2
DATA $1F8F8,$20960,$41EDC,1

LABEL TSCREAD
  REM I:COUNTER, D: DATA, R: RESULT, N:MINIMUM, X:MAXIMUM
  VAR I,D,R,N,X
  R=0
  N=0XFFFF
  X=0
  FOR I=1 TO 10
    D=ARGS(1)
    SPISWAPDATA &D,3
    D=((D AND 0X7F00)>>3) OR ((D AND 0XF80000)>>19)
    R=R+D
    IF D<N THEN N=D
    IF X<D THEN X=D
  NEXT
RETURN (R-N-X)>>3

LABEL MINMAX
  IF LCDTURN=0 OR LCDTURN=180 THEN
    LINE 0,0,30,30:LINE 0,0,10,0:LINE 0,0,0,10
    LINE 289,209,319,239:LINE 309,239,319,239:LINE 319,229,319,239
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ELSE
    LINE 239,0,209,30:LINE 239,0,229,0:LINE 239,0,239,10
    LINE 0,319,30,289:LINE 0,319,10,319:LINE 0,319,0,309
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS "
    PRINT "SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ENDIF
  XMIN=0XFFFF:XMAX=0:YMIN=0XFFFF:YMAX=0
  DO
    IF KEYS(32) THEN BREAK
    IF IN(0) THEN CONTINUE
    X1=GOSUB(TSCREAD,0X94)
    Y1=GOSUB(TSCREAD,0XD4)
    X2=GOSUB(TSCREAD,0X94)
    Y2=GOSUB(TSCREAD,0XD4)
    IF IN(0) THEN CONTINUE
    IF ABS(X1-X2)>2 THEN CONTINUE
    IF ABS(Y1-Y2)>2 THEN CONTINUE
    X=(X1+X2)>>1
    Y=(Y1+Y2)>>1
    IF X<XMIN THEN XMIN=X
    IF XMAX<X THEN XMAX=X
    IF Y<YMIN THEN YMIN=Y
    IF YMAX<Y THEN YMAX=Y
    CURSOR 5,5:PRINT HEX$(XMIN),HEX$(XMAX),
    CURSOR 5,6:PRINT HEX$(YMIN),HEX$(YMAX),
  LOOP
  FOPEN(INITF$,"W")
  FPRINT XMIN
  FPRINT XMAX
  FPRINT YMIN
  FPRINT YMAX
  FCLOSE
RETURN
